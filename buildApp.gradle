import cn.quickits.tools.make.Config
import cn.quickits.tools.make.DpdInfo
import cn.quickits.tools.make.DpdManager

apply {
    plugin "com.android.application"

    plugin "kotlin-android"

    plugin "kotlin-kapt"

    plugin "kotlin-android-extensions"
}

android {
    compileSdkVersion Config.compileSdkVersion

    defaultConfig {
        applicationId Config.applicationId + suffix
        minSdkVersion Config.minSdkVersion
        targetSdkVersion Config.targetSdkVersion
        versionCode Config.versionCode
        versionName Config.versionName

        multiDexEnabled true

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        resValue "string", "app_name", Config.appName + suffix
    }

    buildTypes {
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
        }

        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
        }
    }

    packagingOptions {
        exclude "META-INF/*"
    }

    dexOptions {
        preDexLibraries true
        javaMaxHeapSize "8g"
        maxProcessCount 8
        dexInProcess = true
    }

    compileOptions {
        targetCompatibility JavaVersion.VERSION_1_8
        sourceCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }
}

kapt {
    arguments {
        arg("AROUTER_MODULE_NAME", project.path.replaceAll(":", "_"))
    }
}

dependencies {
    for (Map.Entry<String, DpdInfo> entry : DpdManager.pkgDependencies) {
        runtimeOnly entry.value.dep
    }

    for (Map.Entry<String, DpdInfo> entry : DpdManager.exportDependencies) {
        implementation entry.value.dep
    }

    for (Map.Entry<String, DpdInfo> entry : DpdManager.testDependencies) {
        testImplementation entry.value.dep
    }

    for (Map.Entry<String, DpdInfo> entry : DpdManager.androidTestDependencies) {
        androidTestImplementation entry.value.dep
    }
}

def getSuffix() {
    if (project.path == ":app") return ""
    return "." + project.name.substring("features_".length(), project.name.length() - "_app".length())
}